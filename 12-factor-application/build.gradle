apply plugin: 'war'
apply plugin: 'eclipse-wtp'
apply plugin: 'maven-publish'
apply plugin: 'liberty'

ext {
    libertyBaseDir = project.hasProperty('libertyRoot') ? "${libertyRoot}" : 'C:/Liberty'
    projectLicenseCode = project.hasProperty('libertyLicenseCode') ? "${libertyLicenseCode}" : 'licenseCode'
}

buildscript {
    repositories {
        maven {
            name = 'Sonatype Nexus Snapshots'
            url = 'https://oss.sonatype.org/content/repositories/snapshots/'
        }
        mavenCentral()
    }
    dependencies {
        classpath 'net.wasdev.wlp.gradle.plugins:liberty-gradle-plugin:1.0-SNAPSHOT'
        classpath files('gradle/wlp-anttasks.jar')
    }
}

//Dependencies
repositories { mavenCentral(); }

dependencies {
    providedCompile group:'javax.servlet', name:'javax.servlet-api', version:'3.1.0'
    providedCompile group:'javax.ws.rs', name:'javax.ws.rs-api', version: '2.0.1'
    providedCompile group:'javax.json', name:'javax.json-api', version:'1.0'
    testCompile 'junit:junit:4.12'
    testRuntime 'org.apache.cxf:cxf-rt-rs-client:3.1.1'
    testRuntime 'org.glassfish:javax.json:1.0.4'
}

sourceCompatibility = 1.7

// Set the Eclipse facets to use 3.0 of the Dynamic Web Module
eclipse.wtp.facet {
    facets = []
    facet name: 'jst.java', type: 'fixed'
    facet name: 'jst.web', type: 'fixed'
    facet name: 'jst.java', version: '1.7'
    facet name: 'jst.web', version: '3.0'
}

// Copy WAR into local server folder.
task publishWar(dependsOn: 'jar', type: Copy) {
    from(war)
    into('../12-factor-wlpcfg/servers/12FactorAppServer/apps')
}

liberty {
    wlpDir = "${libertyBaseDir}/wlp"
    serverName = '12FactorAppServer'
    userDir = '../12-factor-wlpcfg'

    install {
        licenseCode = "${projectLicenseCode}"
        version = '8.5.5_06'
        baseDir = "${libertyBaseDir}"
    }

    packageLiberty {
        include = 'usr'
        archive = 'build/libs/12FactorApp.zip'
    }

}

publishing {
    publications {
        mavenWar(MavenPublication) {
            version '1.0-SNAPSHOT'
            groupId 'net.wasdev.wlp.sample'
            artifactId 'microservices-12factor'

            from components.web
        }
    }
}

// Add a functional test task that runs during the 'check' phase that uses the Liberty Gradle plugin
// to launch Liberty and run the test classes in the fvt source set
task fvt(type: Test, dependsOn: jar) {
    group 'Verification'
    description 'Runs the functional verification tests.'
    reports.html.destination = file("$buildDir/reports/fvt")
    reports.junitXml.destination = file("$buildDir/test-results/fvt")
    include '**/fvt/**'
    exclude '**/unit/**'
    systemProperties = ['liberty.test.port': "${libertyPort}"]
    dependsOn publishWar, libertyStart
    finalizedBy libertyStop
}

test {
    reports.html.destination = file("$buildDir/reports/unit")
    reports.junitXml.destination = file("$buildDir/test-results/unit")
    include '**/unit/**'
    exclude '**/fvt/**'
}

assemble.dependsOn('publishWar')

installLiberty.dependsOn('publishWar')

// If both license code and Liberty root are set then install liberty using liberty-gradle plugin
if(project.hasProperty('libertyLicenseCode')) {
    println "Using liberty-gradle plugin to install Liberty to location ${libertyBaseDir}"
    println 'Packaging server but skipping functional tests because default runtime jar doesn\'t contain Java EE 7 features'
    assemble.dependsOn('libertyPackage')
    libertyPackage.dependsOn('installLiberty')
// If only Liberty root is set then we are using a local version
} else if(!project.hasProperty('libertyLicenseCode') && file("${libertyBaseDir}/wlp/lib/ws-launch.jar").exists()) {
    println "Using local Liberty install located at ${libertyBaseDir} to package server and run tests"
    assemble.dependsOn('libertyPackage')
    libertyPackage.dependsOn('publishWar')
    check.dependsOn('fvt')
// If neither license code or Liberty root are set skip steps that require a server
} else {
    println 'No liberty install specified'
    println 'Skipping packaging server and functional tests'
}

libertyStart.dependsOn testClasses
